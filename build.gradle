plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.5' apply false
}

group 'com.github.philippheuer.gcf4j'
description = 'A generic command framework'
version = String.valueOf(System.getenv("CI_COMMIT_REF_NAME")).replace("v", "")

// All-Projects
allprojects {
    // Repositories
    repositories {
        jcenter()
        maven { url "https://dl.bintray.com/philippheuer/maven" }
    }

    // Artifact Info
    project.ext {
        groupId = 'com.github.philippheuer.gcf4j'
        artifactNamespace = 'philippheuer'
        artifactName = 'gcf4j'
        artifactVersion = String.valueOf(System.getenv("CI_COMMIT_REF_NAME")).replace("v", "") + (Boolean.valueOf(System.getProperty("snapshot")) ? "-SNAPSHOT" : "")
        artifactDescription = 'GCF4J'
        websiteUrl = 'https://github.com/philippheuer/gcf4j'
        issueTrackerUrl = 'https://github.com/philippheuer/gcf4j/issues'
        vcsUrl = 'https://github.com/philippheuer/gcf4j.git'
    }
}

// Subprojects
subprojects {
    apply plugin: 'java-library'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    // Source Compatiblity
    sourceCompatibility = 11

    // Dependencies
    dependencies {
        // POM
        implementation platform("io.projectreactor:reactor-bom:Californium-RELEASE")

        // Logging
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
        testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

        // JUnit5
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.6.2'
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.2'

        // Getter & Setter
        compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.12'
        testCompile group: 'org.projectlombok', name: 'lombok', version: '1.18.12'
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.12'
        testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.12'
    }

    test {
        // Enable JUnit 5 (Gradle 4.6+).
        useJUnitPlatform()

        // Always run tests, even when nothing changed.
        dependsOn 'cleanTest'

        // Show test results.
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}
