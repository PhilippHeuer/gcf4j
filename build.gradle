plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'io.freefair.lombok' version '5.3.0' apply false
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.5' apply false
}

group 'com.github.philippheuer.gcf4j'
description = 'A generic command framework'
version = String.valueOf(System.getenv("CI_COMMIT_REF_NAME")).replace("v", "")

// All-Projects
allprojects {
    // Repositories
    repositories {
        jcenter()
        maven { url "https://dl.bintray.com/philippheuer/maven" }
    }

    // Artifact Info
    project.ext {
        groupId = 'com.github.philippheuer.gcf4j'
        artifactNamespace = 'philippheuer'
        artifactName = 'gcf4j'
        artifactVersion = String.valueOf(System.getenv("CI_COMMIT_REF_NAME")).replace("v", "") + (Boolean.valueOf(System.getProperty("snapshot")) ? "-SNAPSHOT" : "")
        artifactDescription = 'GCF4J'
        websiteUrl = 'https://github.com/philippheuer/gcf4j'
        issueTrackerUrl = 'https://github.com/philippheuer/gcf4j/issues'
        vcsUrl = 'https://github.com/philippheuer/gcf4j.git'
    }
}

// Subprojects
subprojects {
    apply plugin: 'java-library'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    // Source Compatiblity
    sourceCompatibility = 11

    // Lombok
    lombok {
        version = '1.18.18'
    }
    generateLombokConfig.enabled = false

    // Dependencies
    dependencies {
        // Logging
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
        testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

        // JUnit5
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.7.1'
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.7.1'
    }

    test {
        // Enable JUnit 5 (Gradle 4.6+).
        useJUnitPlatform()

        // Always run tests, even when nothing changed.
        dependsOn 'cleanTest'

        // Show test results.
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}
